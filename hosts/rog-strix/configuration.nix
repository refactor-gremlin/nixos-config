# Main configuration for ROG Strix G16
# Intel CPU + NVIDIA GPU laptop with ASUS-specific features
{
  inputs,
  lib,
  config,
  pkgs,
  ...
}: {
  imports = [
    # Hardware configuration (generated by nixos-generate-config)
    ./hardware-configuration.nix

    # GPU mode selector (sets myConfig.hardware.nvidia.mode)
    ./gpu-mode.nix
  ];

  # Enable profiles and options
  myConfig.profiles.workstation.enable = true;
  myConfig.services.tailscale.enable = true;
  myConfig.services.tailscale.operator = "jens"; # Allow ktailctl GUI to work
  # advertiseExitNode = false (default) allows using Mullvad or other exit nodes via ktailctl
  myConfig.services.nas.enable = true;
  myConfig.secrets.sshKeyUser = "jens"; # Deploy SSH key to this user

  # Enable ISO support (flake copy, hardware detection)
  myConfig.system.iso.enable = true;
  myConfig.system.iso.hostName = "rog-strix";

  # Hardware configuration
  myConfig.hardware.nvidia.enable = true;
  myConfig.hardware.nvidia.isLaptop = true;
  myConfig.hardware.nvidia.driverBranch = "latest"; # Use 590 driver
  myConfig.hardware.cpu.intel.enable = true;
  myConfig.hardware.asus.enable = true;

  # System configuration
  myConfig.system.power.enable = true;
  myConfig.system.boot.laptop.enable = true;
  myConfig.system.boot.laptop.resumeDevice = "/dev/disk/by-uuid/4d48cb91-7bfa-448e-bc21-93e228ddd729";

  # CachyOS kernel (gaming-optimized) from chaotic-nyx
  # NOTE: Must set boot.kernelPackages directly instead of using
  # myConfig.system.boot.laptop.kernelPackages due to a weird Nix evaluation bug.
  # Using the custom option triggers "amdgpu-pro was removed" error even though
  # amdgpu-pro isn't referenced anywhere. Setting boot.kernelPackages directly works.
  boot.kernelPackages = pkgs.linuxPackages_cachyos;

  # Nixpkgs configuration
  nixpkgs = {
    overlays = [
      inputs.self.overlays.additions
      inputs.self.overlays.modifications
      inputs.self.overlays.stable-packages
      inputs.chaotic.overlays.default # CachyOS packages overlay
    ];
    config = {
      allowUnfree = true;
    };
  };

  # Nix settings
  nix = let
    flakeInputs = lib.filterAttrs (_: lib.isType "flake") inputs;
  in {
    settings = {
      experimental-features = "nix-command flakes";
      flake-registry = "";
      nix-path = config.nix.nixPath;
      download-buffer-size = 67108864; # 64MB - Fixes "download buffer is full" warning
      trusted-users = ["root" "jens" "@wheel"];
      # Binary caches for precompiled packages
      substituters = [
        "https://cache.nixos.org"
        "https://chaotic-nyx.cachix.org"
      ];
      trusted-public-keys = [
        "cache.nixos.org-1:6NCHdD59X431o0gWypbMrAURkbJ16ZPMQFGspcDShjY="
        "chaotic-nyx.cachix.org-1:HfnXSw4pj95iI/n17rIDy40agHj12WfF+Gqk6SonIT8="
      ];
    };
    channel.enable = false;
    registry = lib.mapAttrs (_: flake: {inherit flake;}) flakeInputs;
    nixPath = lib.mapAttrsToList (n: _: "${n}=flake:${n}") flakeInputs;
  };

  # Hostname
  networking.hostName = "rog-strix";

  # Administrator rights
  security.sudo.wheelNeedsPassword = false;

  # User configuration
  users.users.jens = {
    isNormalUser = true;
    description = "Jens";
    shell = pkgs.zsh;
    extraGroups = [
      "wheel"
      "networkmanager"
      "video"
      "audio"
      "docker"
    ];
  };

  # Enable zsh system-wide (required for user shell)
  programs.zsh.enable = true;
  programs.git.enable = true;
  programs.git.config.safe.directory = "/etc/nixos";

  # https://nixos.wiki/wiki/FAQ/When_do_I_update_stateVersion
  system.stateVersion = "26.05";
}
